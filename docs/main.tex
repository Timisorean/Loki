\documentclass[]{article}
\usepackage[a4paper, margin=0.5in]{geometry}
\usepackage{amsmath}
\usepackage{syntax}
\usepackage[T1]{fontenc}
\setlength{\grammarparsep}{2pt plus 1pt minus 1pt} % increase separation between rules
\setlength{\grammarindent}{20em} % increase separation between LHS/RHS

\title{Loki - Implemented PDDL Grammar Definition}
\author{Dominik Drexler}
\date{\today}

\newcommand{\plus}{\textsuperscript{+}}
\newcommand{\typing}{\textsuperscript{:typing}~}
\newcommand{\fluents}{\textsuperscript{:fluents}~}
\newcommand{\constraints}{\textsuperscript{:constraints}~}
\newcommand{\negativepreconditions}{\textsuperscript{:negative-preconditions}~}
\newcommand{\disjunctivepreconditions}{\textsuperscript{:disjunctive-preconditions}~}
\newcommand{\existentialpreconditions}{\textsuperscript{:existential-preconditions}~}
\newcommand{\universalpreconditions}{\textsuperscript{:universal-preconditions}~}
\newcommand{\conditionaleffects}{\textsuperscript{:conditional-effects}~}

\begin{document}
\maketitle

\section{Domain}

% https://tex.stackexchange.com/questions/24886/which-package-can-be-used-to-write-bnf-grammars
\begin{grammar}
    <digit> ::= 0..9

    <letter> ::= a..z | A..Z

    <any char> ::= <letter> | <digit> | - | _

    <name> ::= <letter> <any char>*

    <variable> ::= ?<name>

    <number> ::= digit\plus . digit*   %% TODO int or float

    <term> ::= <name> | <variable>


    <type> ::= <name> | ( either <type>\plus ) | ( fluent <type> )

    <type> ::= ( <type> )

    <typed list (x)> ::= x*

    <typed list (x)> ::= x\plus - <type> <typed list (x)>


    <predicate> ::= <name>

    <atomic formula skeleton> ::= ( <predicate> <typed list (variable)> )



    <function symbol> ::= <name>

    <function type> ::= <digit>

    <atomic function skeleton> ::= ( <function symbol> <typed list (variable)> )

    <function typed list (x)> ::= x*

    <function typed list (x)> ::=\typing{} x\plus - <function type> <function typed list (x)>


    <atomic formular (t)> ::= ( <predicate> t* )

    <atom> ::= <atomic formula (term)>

    <negated atom> ::= ( not <atomic formula (term)> )

    <literal> ::= <atom> | <negated atom>


    <require-def> ::= :strip | :typing

    <types-def> ::= ( :types <typed list (name)> )

    <constants-def> ::= ( :constants <typed list (name)> )

    <predicates-def> ::= ( :predicates <atomic formula skeleton>* )

    <functions-def> ::= ( :functions <function typed list (atomic function skeleton)> )

    <constraints-def> ::=\constraints ( :constraints <con-GD> )

    <domain> ::= ( define ( domain <name> )\newline
    [<require-def>] \newline
    [<types-def>] \newline
    [<constants-def>] \newline
    [<predicates-def>] \newline
    [<constraints-def>] \newline
    [<structure-def>*] )

\end{grammar}

\section{Actions}

\begin{grammar}
    <atomic formula (t)> ::= ( <predicate> t* )

    <atom> ::= <atomic formula (term)>

    <negated atom> ::= ( not <atom> )

    <literal> ::= <atom> | <negated atom>


    <multi-op mul> ::= *

    <multi-op plus> ::= +

    <multi-op> ::= <multi-op mul> | <multi-op plus>

    <binary-op minus> ::= -

    <binary-op div> ::= /

    <binary-op> ::= <binary-op minus> | <binary-op div> | <multi-op>


    <binary-comp greater> ::= >

    <binary-comp less> ::= \textless

    <binary-comp equal> ::= =

    <binary-comp greater equal> ::= >=

    <binary-comp less equal> ::= \textgreater=

    <binary-comp> ::= <binary-comp greater> | <binary-comp less> \newline
        | <binary-comp equal> | <binary-comp greater equal> \newline
        | <binary-comp less equal>


    <f-exp number> ::= <number>

    <f-exp binary op> ::= ( <binary-op> <f-exp> <f-exp> )

    <f-exp minus> ::= ( - <f-exp> )

    <f-exp head> ::= <function-symbol> | ( <function-symbol> <term>* )

    <f-exp> ::= <f-exp number> | <f-exp binary op> | <f-exp minus> | <f-exp head>

    % Preconditions
    <GD atom> ::= <atom>

    <GD literal> ::=\negativepreconditions{} <literal>

    <GD and> ::=  ( and <GD>* )

    <GD or> ::=\disjunctivepreconditions{} ( or <GD>* )

    <GD not> ::=\disjunctivepreconditions{} ( not <GD>* )

    <GD imply> ::=\disjunctivepreconditions{} ( imply <GD> <GD> )

    <GD exists> ::=\existentialpreconditions{} ( exists <typed list (variable)> <GD> )

    <GD forall> ::=\universalpreconditions{} ( forall <typed list (variable)> <GD> )

    <GD f-comp> ::=\fluents{} ( <binary-comp> <f-exp> <f-exp> )

    <GD> ::= <GD atom> | <GD literal> | <GD and> | <GD or> | <GD not> | <GD imply> | <GD exists> | <GD forall> | <GD f-comp>

    <pref-name> ::= name

    <pre-GD simple> ::= <GD>

    <pre-GD and> ::= ( and <pre-GD>* )

    <pre-GD forall> ::= ( forall <typed list (variable)> <pre-GD> )

    <pre-GD preferences> ::= ( preference <pref-name> <GD> )

    <pre-GD> ::= <pre-GD simple> | <pre-GD and> | <pre-GD forall> | <pre-GD preferences>


    % Effects
    <assign-op> ::= assign

    <assign-op> ::= scale-up

    <assign-op> ::= scale-down

    <assign-op> ::= increase

    <assign-op> ::= decrease

    <simple-effect-literal> ::= <literal>

    <simple-effect-fluent> ::=\fluents ( <assign-op> <f-head> <f-exp> )

    <simple-effect> ::= <simple-effect-literal> | <simple-effect-fluent>

    <cond-effect-forall> ::=\conditionaleffects{} ( forall ( <typed list (variable)>* ) <effect> )

    <cond-effect-when> ::=\conditionaleffects{} ( when <GD> <effect> )

    <cond-effect> ::=\conditionaleffects{} <cond-effect-forall> | <cond-effect-when>

    <effect and> ::= ( and <effect>* )

    <effect> ::= <simple-effect> | <cond-effect> | <effect and>
\end{grammar}

\section{Problem}

\begin{grammar}
    <con-GD> ::= ( and <con-GD>* )

    <con-GD> ::= (forall ( <typed list (variable)> ) <con-GD> )

    <con-GD> ::= ( at end <GD> )

    <con-GD> ::= ( always <GD> )

    <con-GD> ::= ( sometime <GD> )

    <con-GD> ::= ( within <number> <GD> )

    <con-GD> ::= ( at-most-once <GD> )

    <con-GD> ::= ( sometime-after <GD> <GD> )

    <con-GD> ::= ( sometime-before <GD> <GD> )

    <con-GD> ::= ( always-within <number> <GD> <GD> )

    <con-GD> ::= ( hold-during <number> <number> <GD> )

    <con-GD> ::= ( hold-after <number> <GD> )
\end{grammar}

\nocite{mcdermott-et-al-1998}

\bibliographystyle{plain}
\bibliography{refs}

\end{document}